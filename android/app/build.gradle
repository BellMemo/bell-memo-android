plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.example.bellmemo"
    compileSdk flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.example.bellmemo"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            // 启用代码混淆和资源压缩
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source '../..'
}

configurations.all {
    resolutionStrategy {
        force 'androidx.browser:browser:1.8.0'
        force 'androidx.core:core-ktx:1.13.1'
        force 'androidx.core:core:1.13.1'
    }
}

dependencies {
    // Flutter 的 Play Store deferred components 会引用 Play Core（SplitCompat / SplitInstall*）
    // 否则 R8 会报 Missing class com.google.android.play.core.*
    implementation 'com.google.android.play:core:1.10.3'

    // Force downgrade dependencies that require bleeding edge AGP
    constraints {
        implementation('androidx.browser:browser:1.8.0') {
            because '1.9.0 requires AGP 8.9.1'
        }
        implementation('androidx.core:core-ktx:1.13.1') {
            because '1.17.0 requires AGP 8.9.1'
        }
        implementation('androidx.core:core:1.13.1') {
            because '1.17.0 requires AGP 8.9.1'
        }
    }
}

